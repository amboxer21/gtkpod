dnl Process this file with autoconf to produce a configure script.

# Require autoconf 2.54 or higher
AC_PREREQ(2.54)

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(gtkpod, 0.73-CVS)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE
AC_CANONICAL_HOST
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC

dnl Only use -Wall if we have gcc
if test "x$GCC" = "xyes"; then
  if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
    CFLAGS="$CFLAGS -Wall"
  fi
fi

GETTEXT_PACKAGE=gtkpod
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",["Gettext package name"])

AC_CHECK_PROG([have_pkgconfig], [pkg-config], ["ok"])

if test "y$have_pkgconfig" = "y"; then
	AC_MSG_FAILURE([**** pkg-config required (http://www.freedesktop.org/software/pkgconfig)])
fi

PKG_CHECK_MODULES(PACKAGE, [gtk+-2.0 >= 2.0.0 gthread-2.0 >= 0.14.0 glib-2.0 > 2.0.0])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

dnl used in itunesdb.c to figure out whether we run gtkpod
AC_DEFINE_UNQUOTED([IS_GTKPOD], 1, [Define this to indicate we are compiling gtkpod])

dnl we need 'flex'
AM_PROG_LEX

dnl Retrieve the path of mount and umount binaries
AC_PATH_PROG(MOUNT, mount)
AC_SUBST(MOUNT)
AC_PATH_PROG(UMOUNT, umount)
AC_SUBST(UMOUNT)

dnl Add the languages which your application supports here.
ALL_LINGUAS="de fr it ja"
AM_GLIB_GNU_GETTEXT

dnl Check if we have to supply getopt_long
dnl If getopt_long_only is not available, getopt.c and getopt1.c
dnl (providing getopt_long*) will be included into the binary.
AC_CHECK_FUNCS(getopt_long_only, , [AC_LIBOBJ(getopt)]) 
AC_CHECK_FUNCS(getopt_long_only, , [AC_LIBOBJ(getopt1)]) 

dnl Check for libid3tag
AC_SEARCH_LIBS(id3_tag_new,
	["id3tag" "id3tag -lz"],,
	AC_MSG_ERROR([*** id3tag lib not found]))

dnl Check for libmp4v2 (and mp4.h)
AC_SEARCH_LIBS(MP4FileInfo,
	["mp4v2" "mp4v2 -lstdc++" "mp4v2 -lz" "mp4v2 -lz -lstdc++"],
	[AC_CHECK_HEADER(mp4.h,
	  [have_mp4v2="yes -- will build with aac support" AC_DEFINE_UNQUOTED(HAVE_LIBMP4V2, 1, ["Define to 1 if you have the mp4v2 library"])],
	  [echo "*** mp4.h cannot be found. Check your mp4v2 installation."])])

if test "y$have_mp4v2" = "y"; then
	have_mp4v2="*** no -- will build without aac support"
fi

dnl Check for Linux-specific headers (so we can compile Linux-specific
dnl stuff only when compiling under Linux)
AC_CHECK_HEADERS(linux/cdrom.h scsi/scsi.h scsi/scsi_ioctl.h)

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
scripts/Makefile
])

echo "
Configuration for $PACKAGE $VERSION :
--------------------------------

 Host System Type .....: $host
 Install path .........: $prefix
 Preprocessor .........: $CC $CPPFLAGS
 Compiler .............: $CC $CFLAGS
 Linker ...............: $CC $LDFLAGS $LIBS
 GTK2 version .........: `pkg-config gtk+-2.0 --modversion`
 GLib2/GThread version : `pkg-config gthread-2.0 --modversion`
 id3tag lib ...........: yes (`pkg-config id3tag --modversion`)
 mp4v2 ................: $have_mp4v2
 NLS/gettext ..........: $USE_NLS

 Now type 'make' to build $PACKAGE $VERSION,
 and then 'make install' for installation.
"
