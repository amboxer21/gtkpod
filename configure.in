dnl Process this file with autoconf to produce a configure script.

# Require autoconf 2.55 or higher
AC_PREREQ(2.55)

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(gtkpod, 0.99.1)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE
AC_CANONICAL_HOST
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC

dnl Only use -Wall if we have gcc
if test "x$GCC" = "xyes"; then
  if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
    CFLAGS="$CFLAGS -Wall"
  fi
fi

GETTEXT_PACKAGE=gtkpod
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",["Gettext package name"])

dnl check if pkg-config exists -- if not print a meaningful error message
AC_CHECK_PROG([have_pkgconfig], [pkg-config], ["ok"])

if test "y$have_pkgconfig" = "y"; then
	AC_MSG_FAILURE([**** pkg-config required (http://www.freedesktop.org/software/pkgconfig)])
fi

dnl check for libs that are managed with pkg-config
PKG_CHECK_MODULES(PACKAGE, [gtk+-2.0 >= 2.4.0 gthread-2.0 >= 0.14.0 glib-2.0 > 2.4.0 libglade-2.0  >=  2.4.0 gmodule-2.0 libgpod-1.0 >= 0.3.0],,[AC_MSG_FAILURE([*** $PACKAGE_PKG_ERRORS])])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

dnl we need 'flex'
AM_PROG_LEX
if ! test "y$LEX" = "yflex"; then
  if ! test "y$LEX" = "ylex"; then
    AC_MSG_FAILURE([**** flex or lex required])
  fi
fi

dnl Retrieve the path of mount and umount binaries
AC_PATH_PROG(MOUNT, mount)
AC_SUBST(MOUNT)
AC_PATH_PROG(UMOUNT, umount)
AC_SUBST(UMOUNT)

dnl Add the languages which your application supports here.
ALL_LINGUAS="de fr he it ja sv"
AM_GLIB_GNU_GETTEXT

dnl Check if we have to supply getopt_long
dnl If getopt_long_only is not available, getopt.c and getopt1.c
dnl (providing getopt_long*) will be included into the binary.
AC_CHECK_FUNCS(getopt_long_only, , [AC_LIBOBJ(getopt)]) 
AC_CHECK_FUNCS(getopt_long_only, , [AC_LIBOBJ(getopt1)]) 

dnl Check if flock() is available (seems to be missing on some
dnl solaris versions)
AC_CHECK_FUNCS(flock)

dnl Check if statvfs() is available (otherwise we fall back on
dnl 'df' to determine free space on the iPod
AC_CHECK_FUNCS(statvfs)

dnl Check for libid3tag
AC_CHECK_LIB([id3tag], [id3_frame_field], ,
             AC_MSG_ERROR([*** id3tag >= 0.15 lib not found (0.14 will not work!)]))

AC_CHECK_LIB([curl],  [curl_easy_init], ,
	     AC_MSG_ERROR([*** curl lib not found!]))

dnl Check for libmp4v2 (and mp4.h)
AC_SEARCH_LIBS(MP4FileInfo,
	["mp4v2" "mp4v2 -lstdc++" "mp4v2 -lz" "mp4v2 -lz -lstdc++"],
	[AC_CHECK_HEADER(mp4.h,
	  [have_mp4v2="yes -- will build with aac support" AC_DEFINE_UNQUOTED(HAVE_LIBMP4V2, 1, ["Define to 1 if you have the mp4v2 library"])],
	  [echo "*** mp4.h cannot be found. Check your mp4v2 installation."])])

if test "y$have_mp4v2" = "y"; then
	have_mp4v2="*** no -- will build without aac support"
fi

dnl Additional libs maybe needed when compiling under solaris
AC_SEARCH_LIBS(bind,
	      ["socket" "nsl" "socket -lnsl"])

dnl Check for Linux-specific headers (so we can compile Linux-specific
dnl stuff only when compiling under Linux)
AC_CHECK_HEADERS(linux/cdrom.h scsi/sg.h scsi/scsi.h scsi/scsi_ioctl.h)

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
scripts/Makefile
])

echo "
Configuration for $PACKAGE $VERSION :
--------------------------------

 Host System Type .....: $host
 Install path .........: $prefix
 Preprocessor .........: $CC $CPPFLAGS
 Compiler .............: $CC $CFLAGS $PACKAGE_CFLAGS
 Linker ...............: $CC $LDFLAGS $LIBS $PACKAGE_LIBS
 GTK2 version .........: `pkg-config gtk+-2.0 --modversion`
 GLib2/GThread version : `pkg-config gthread-2.0 --modversion`
 id3tag lib ...........: yes
 curl lib .............: yes
 mp4v2 ................: $have_mp4v2
 NLS/gettext ..........: $USE_NLS

 Now type 'make' to build $PACKAGE $VERSION,
 and then 'make install' for installation.
"
